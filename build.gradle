plugins {
	id "de.undercouch.download" version "3.2.0"
	id "nebula.lint" version "8.2.2"
	id "net.saliman.properties" version "1.4.6"
	id "application"
}

import OperatingSystem

ext {
	if (!project.hasProperty("operatingSystem")) {
		operatingSystem = OperatingSystem.current().toString()
	}
}

def browser = hasProperty("browser") ? browser : "firefox"
def browserScript = file("gradle/${browser}-env.gradle")

if (!browserScript.exists()) {
	throw new GradleException("Unknown browser: ${browser}")
}

apply from: browserScript

gradleLint.rules = ["all-dependency"]

dependencies {
	compile "org.seleniumhq.selenium:selenium-firefox-driver:${seleniumVersion}"
	compile "org.seleniumhq.selenium:selenium-chrome-driver:${seleniumVersion}"
	compile "org.seleniumhq.selenium:selenium-ie-driver:${seleniumVersion}"
	compile "org.seleniumhq.selenium:selenium-api:${seleniumVersion}"
	compile "org.seleniumhq.selenium:selenium-support:${seleniumVersion}"
	runtime "ch.qos.logback:logback-classic:1.2.3"
}

run {
	mainClassName = "me.khyen.test.JavaSandBox"

	if (project.hasProperty("execArgs")) {
		args project.getProperty("execArgs").split()
	}

	environment("browser", browser)
	environment("jenkinsUsername", jenkinsUsername)
	environment("jenkinsPassword", jenkinsPassword)
}

repositories {
	jcenter()
}